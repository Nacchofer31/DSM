-- @path uml=/DSM-E2/Metamodels/dsmUML.ecore
-- @path er=/DSM-E2/Metamodels/MyER.ecore

module Ejer7;
create OUT : ER from IN : UML;

rule Diagrama2Esquema {
	from
		d : UML!Diagrama
	to 
		e : ER!Esquema (
			Nombre <- d.nombre,
			Tablas <- d.clases,
			clavesAjenas <- d.asociaciones
		)
}

rule Clase2Tabla {
	from
		c : UML!Clase (c.csuper.oclIsUndefined())
	to 
		t : ER!Tabla (
			Nombre <- c.nombre,
			columnas <- col,
			columnas <- c.atributos
		),
		col : ER!Columna (
			Nombre <- 'ID_' + c.nombre,
			EsPrimaria <- true,
			Tipo <- #integer
		)
}

rule ClaseHija2Tabla {
	from
		c : UML!Clase (not c.csuper.oclIsUndefined())
	to 
		t : ER!Tabla (
			Nombre <- c.nombre,
			columnas <- col,
			columnas <- c.atributos
		),
		col : ER!Columna (
			Nombre <- 'ID_' + c.nombre,
			EsPrimaria <- true,
			Tipo <- #integer,
			enClaveAjena <- fk
		),
		fk : ER!ClaveAjena (
			Nombre <- 'ca_de_' + c.nombre + '_a_' + c.csuper.nombre,
			columna <- fk_col,

			columnaReferenciada <- thisModule.resolveTemp(c.csuper, 'col'),
			esquema <- t.Padre
		),
		fk_col : ER!Columna (
			Nombre <- 'id_fk_' + c.csuper.nombre,
			tabla <- t
		)
}

rule Atributo2Columna {
	from 
		a : UML!Atributo
	to	
		col : ER!Columna (
			Nombre <- a.nombre,
			EsPrimaria <- false,
			Tipo <- if (a.tipo.toString() = 'entero')
				    then #integer 
				    else if (a.tipo.toString() = 'real') 
					     then #double 
				         else #string 
					     endif
				    endif
		)
}


rule Asociacion2FK { -- De 1 a 1 o de 1 a N
	from 
		a : UML!Asociacion (a.finrelacion.first().maxCard.toString() = '1')
	to 
		fk : ER!ClaveAjena (
			Nombre  <- 'ca_de_' + a.finrelacion.last().rol + '_a_' + a.finrelacion.first().rol,
			columna <- fk_columna,
			
			columnaReferenciada <- thisModule.resolveTemp(a.finrelacion.first().clase, 'col')
		),
		fk_columna : ER!Columna (
			Nombre <- 'ID_FK_' + a.finrelacion.first().rol,
			tabla <- a.finrelacion.last().clase
		)
}

rule Asociacion2FK2 { -- De N a 1
	from 
		a : UML!Asociacion (a.finrelacion.last().maxCard.toString() = '1' and a.finrelacion.first().maxCard.toString() = '*')
	to 
		fk : ER!ClaveAjena (
			Nombre <- 'ca_de_' + a.finrelacion.first().rol + '_a_' + a.finrelacion.last().rol,
			columna <- fk_columna,
			columnaReferenciada <- thisModule.resolveTemp(a.finrelacion.last().clase, 'col')
		),
		fk_columna : ER!Columna (
			Nombre <- 'ID_FK_' + a.finrelacion.last().rol,
			tabla <- a.finrelacion.first().clase
		)
}

rule Asociacion2FK3 { -- De N a N
	from
		a : UML!Asociacion (a.finrelacion.first().maxCard.toString() = '*' and a.finrelacion.last().maxCard.toString() = '*')
	to 
		fk_1 : ER!ClaveAjena (
			Nombre <- 'ca_de_' + a.finrelacion.first().rol + '_a_' + a.finrelacion.last().rol,
			columna <- fk_columna_1,
			columnaReferenciada <- thisModule.resolveTemp(a.finrelacion.last().clase, 'col')
		),
		fk_2 : ER!ClaveAjena (
			Nombre <- 'ca_de_' + a.finrelacion.last().rol + '_a_' + a.finrelacion.first().rol,
			columna <- fk_columna_2,
			columnaReferenciada <- thisModule.resolveTemp(a.finrelacion.first().clase, 'col'),
			esquema <- fk_1.esquema
		),
		fk_columna_1 : ER!Columna (
			Nombre <- 'ID_FK_' + a.finrelacion.last().rol,
			tabla <- t
		),
		fk_columna_2 : ER!Columna (
			Nombre <- 'ID_FK_' + a.finrelacion.first().rol,
			tabla <- t
		),
		t : ER!Tabla (
			Nombre <- a.finrelacion.first().rol + '_' + a.finrelacion.last().rol,
			Padre <- fk_1.esquema
		)
}